
/******************************************************************************************
 *
 * PS BOOSTER. Directory: /afs/cern.ch/eng/ps/cps/Psb
 * It consists of 16 identical periods, apart from the equipment
 * in the straight sections.
 * The BOOSTER has a radius of 25m and thus a circonference of 157.08m
 *
 * The injected beam from the linac have Ekin=50 MeV proton beam with p~300MeV/c.
 * (311 MeV/c according to the report: http://accelconf.web.cern.ch/accelconf/p05/PAPERS/TPAT054.PDF)
 * corresponding to E=0.988471 GeV and E0=0.938272 GeV. (The rest mass of a proton is 0.938272 GeV)
 *
 * The kinetic energy of the extracted beam is Ekin=1.4 GeV and p=2.141766 GeV/c [p=(SQRT(Ekin^2+2*E0*Ekin)/c]
 * The beam is extracted to the PS. In the PS, it is injected in SS42.
 *
 *
 * Revolution time at extraction:  T,revolution,ext = 572.79 ns (nano  seconds)
 * Revolution time at injection:   T,revolution,inj =   1.6  us (micro seconds)
 * The BOOSTER today has 2 bunches in each ring, i.e. 8 bunches in the PS.
 * Injection time: C=275 ms, measure time C=290 ms
 *
 * Working points:
 * Injection - high intensity          : QX=4.28,  QY=4.55
 * Injection - low  intensity e.g. LHC : QX=4.28,  QY=4.45
 * Extraction                          : QX=4.172, QY=4.23   = wp1
 *
 *
 *
 *
 * The 2 MHz cavities are for the H0 mode, they are run around 8kV.
 * They are run at the revolution frequency.
 * The beam is below transition.
 *
 * The 4 MHz cavities are used to flatten the bunches.
 * They reduce the peak current compared to the average currents.
 * The 4 MHz cavities are run between 6-8 kV.
 * They are run at twice the revolution frequency.
 *
 *
 * All elements in the straight sections added.  21 Dec 2006 O.Berrig
 * New working point: QX = 4.172 and QY = 4.23.  21 Dec 2006 O.Berrig
 *
 *
 ******************************************************************************************
 *
 * This file is for protons at 0.348 GeV/c at time c = 301
 *
 *
 * Execute with:  >madx_dev < psb_orbit.madx
 *
 ******************************************************************************************/



 title, 'BOOSTER lattice';

 option, echo;
 option, RBARC=FALSE;



/******************************************************************************************
 * BOOSTER
 ******************************************************************************************/
 call, file = '../psb.ele';
 call, file = '../psb.seq';
 call, file = '../psb.dbx';
 call, file = '../strength/psb_orbit.str';



!save, sequence=psb1, file=psb1.save;



/******************************************************************************************
 * beam, use
 ******************************************************************************************/
beam, particle=PROTON, pc=0.348;  ! change depending on c-time of orbit measurement; pc=momentum
use, sequence=psb1;

set,  format="20.10f";



/******************************************************************************************
 * Match for new working point
 ******************************************************************************************/

MATCH,sequence=psb1;
 vary, NAME=kKF, step = 0.0001;
 vary, NAME=kKD, step = 0.0001;
 constraint, range=#E, MUX=4.172, MUY=4.230;
 lmdif, calls = 10000, tolerance = 1.0E-21;
ENDMATCH;


! Print results on file: match_orbit.prt
assign, echo="../output/match_orbit.prt";
print, text="match_orbit";
value,  kKF;
value,  kKD;
assign, echo=terminal;





/******************************************************************************************
 * TWISS
 ******************************************************************************************/

PSHIFT=0;

/************************************************************
 * MAD uses pt as the 5th variable NOT delta-p.
 * The result is that all derivatives are with repect to pt.
 * This is the reason for the non-standard dispersion values
 * for non-relativistic machines, like the PSB
 ************************************************************/
beta=sqrt(1-1/beam->gamma^2);
disp:=beta*table(twiss,dx); ! Delta_x=Disp*Delta_p/p;


select, flag=twiss, clear;
select, flag=twiss, column=name, s,x,alfx,alfy,betx,bety,disp;

twiss ,centre
      , DELTAP = PSHIFT
      , table=TWISS
      , file='../output/output_orbit.outx';

value,table(TWISS,PSB1$START,betx);

show, BR.BHZ11;


/******************************************************************************************
 * PLOT
 ******************************************************************************************/

/* Use >gv psb_orbit.ps to plot */

PLOT, table=TWISS, VAXIS=BETX,BETY,HAXIS=S,
      SPLINE=FALSE, COLOUR=100, RANGE=#S/#E, FILE='../output/psb_orbit';
PLOT, table=TWISS, VAXIS=DISP,HAXIS=S,
      SPLINE=FALSE, NOVERSION=TRUE, COLOUR=100, RANGE=#S/#E;
PLOT, table=TWISS, VAXIS=ALFX,ALFY,HAXIS=S,
      SPLINE=FALSE, NOVERSION=TRUE, COLOUR=100, RANGE=#S/#E;


STOP;

